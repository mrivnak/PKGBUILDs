# Maintainer: Mike Rivnak <rivnakm1@gmail.com>

pkgname="systemd-boot-populate-git"
_pkgname="systemd-boot-populate"
pkgver=20210905.r25
pkgrel=1
arch=(
	'x86_64'
	'i686'
	'aarch64'
	'armhf'
	)
pkgdesc="A script that automatically populates systemd-boot loader entries from installed kernels"
url="https://github.com/mrivnak/systemd-boot-populate"
license=('GPL3')
depends=(
	'python'
	'python-argparse'
	'python-jinja'
	'python-termcolor'
	'python-toml'
	)
makedepends=('git')
conflicts=($_pkgname)
provides=($_pkgname)
source=("${pkgname}::git+${url}.git")
sha512sums=('SKIP')
backup=(etc/systemd-boot-populate.toml)

pkgver() {
	cd ${pkgname}
	printf "%s.r%s" "$(git show -s --format=%ci main | sed "s/\ .*//g;s/-//g")" "$(git rev-list --count HEAD)"
}

package() {
	cd ${pkgname}

	install -d -m 755 $pkgdir/etc
	install -d -m 755 $pkgdir/usr/bin

	make DESTDIR="$pkgdir" install
	install -d -m 755 $pkgdir/usr/share/libalpm/hooks
	install -m 644 95-systemd-boot-populate.hook $pkgdir/usr/share/libalpm/hooks/95-systemd-boot-populate.hook
}
